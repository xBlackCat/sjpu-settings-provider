apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'org.xblackcat.sjpu'
def projectTitle = 'SJPU Settings provider library'

version = new File('build.txt').getText('UTF-8')
ext.commonManifest = manifest {
    attributes(
            "Implementation-Title": projectTitle,
            "Implementation-Version": version
    )
}

jar {
    manifest = project.manifest {
        from commonManifest
    }
}

defaultTasks 'clean', 'buildAll'

compileJava.options.encoding = 'UTF-8'
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.xblackcat.sjpu', name: 'sjpu-rcb', version: '[0.2.2,1.0)'
    compile group: 'org.xblackcat.sjpu', name: 'sjpu-utils', version: '[0.1.4,1.0)'
    testCompile group: 'junit', name: 'junit', version: '[4.12,)'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
    manifest = project.manifest {
        from commonManifest
    }
}

javadoc {
    title = projectTitle
}

task javadocJar(dependsOn: javadoc, type: Jar) {
    from javadoc.outputs.files
    classifier = 'javadoc'
    manifest = project.manifest {
        from commonManifest
    }
}

//[compileJava, compileTestJava]*.options.collect {options ->
//  options.deprecation = true
//  options.compilerArgs.add '-Xlint'
//  options.compilerArgs.add '-Xlint:-unchecked'
//  options.compilerArgs.add '-Xlint:-serial'
//  options.compilerArgs.add '-Xlint:-rawtypes'
//}


task sourcesZip(type: Zip) {
    from project.projectDir
    include 'src/**'
    include 'test/**'
    include 'build.*'
    include 'LICENSE'
    into('lib') {
        from configurations.testCompile
        include '*'
    }
    classifier = 'sources'
}

task binZip(dependsOn: 'jar', type: Zip) {
    from jar.outputs.files
    into('dep') {
        from configurations.compile
    }
    classifier = 'bin'
}

sourceSets {
    main {
        java {
            srcDir 'src/java'
        }
    }
    test {
        java {
            srcDir 'test/java'
        }
        resources {
            srcDir 'test/res'
        }
    }
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: mavenCentralUrl) {
                authentication(userName: mavenCentralUser, password: mavenCentralPassword)
            }

            pom.project {
                 name projectTitle
                 packaging 'jar'
                 description 'Utility class for loading properties from files/environment into data objects, declared as interfaces'
                 url 'https://github.com/xBlackCat/sjpu-settings-provider'

                 scm {
                     url 'scm:git@github.com:xBlackCat/sjpu-settings-provider.git'
                     connection 'scm:git@github.com:xBlackCat/sjpu-settings-provider.git'
                     developerConnection 'scm:git@github.com:xBlackCat/sjpu-settings-provider.git'
                 }

                 licenses {
                     license {
                         name 'The Apache Software License, Version 2.0'
                         url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                         distribution 'repo'
                     }
                 }

                 developers {
                     developer {
                         id 'xBlackCat'
                         name 'Zhukov Alexei'
                         email 'xblackcat@gmail.com'
                     }
                 }
             }

        }
    }
}

task buildAll(dependsOn: ['build', 'sourcesJar', 'binZip', 'sourcesZip', 'javadocJar']) {}