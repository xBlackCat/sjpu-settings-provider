apply plugin: 'java'

sourceCompatibility = 1.8
targetCompatibility = 1.8

version = new File('build.txt').getText('UTF-8')
def outputBaseName = 'sjpu-utils'
def projectTitle = 'SJPU Utils'
ext.commonManifest = manifest {
    attributes(
            "Implementation-Title": projectTitle,
            "Implementation-Version": version
    )
}


jar {
    baseName = outputBaseName
    manifest = project.manifest {
        from commonManifest
    }
}

defaultTasks 'clean', 'buildAll'

compileJava.options.encoding = 'UTF-8'
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.+'
    compile group: 'commons-logging', name: 'commons-logging', version: '1.+'
    compile group: 'org.javassist', name: 'javassist', version: '[3.19,)'
    testCompile group: 'junit', name: 'junit', version: '4.+'
}

task sourcesJar(type: Jar) {
    baseName = outputBaseName
    from sourceSets.main.allJava
    classifier = 'sources'
    manifest = project.manifest {
        from commonManifest
    }
}

javadoc {
    title = projectTitle
}

task docsJar(dependsOn: javadoc, type: Jar) {
    baseName = outputBaseName
    from javadoc.outputs.files
    classifier = 'docs'
    manifest = project.manifest {
        from commonManifest
    }
}

//[compileJava, compileTestJava]*.options.collect {options ->
//  options.deprecation = true
//  options.compilerArgs.add '-Xlint'
//  options.compilerArgs.add '-Xlint:-unchecked'
//  options.compilerArgs.add '-Xlint:-serial'
//  options.compilerArgs.add '-Xlint:-rawtypes'
//}


task sourcesZip(type: Zip) {
    baseName = outputBaseName
    from project.projectDir
    include 'src/**'
    include 'test/**'
    include 'build.*'
    include 'LICENSE'
    into('lib') {
        from configurations.testCompile
        include '*'
    }
    classifier = 'sources'
}

task binZip(dependsOn: 'jar', type: Zip) {
    baseName = outputBaseName
    from jar.outputs.files
    into('dep') {
        from configurations.compile
    }
    classifier = 'bin'
}

sourceSets {
    main {
        java {
            srcDir 'src/java'
        }
    }
    test {
        java {
            srcDir 'test/java'
        }
        resources {
            srcDir 'test/res'
        }
    }
}

task buildAll(dependsOn: ['build', 'sourcesJar', 'binZip', 'sourcesZip', 'docsJar']) {}